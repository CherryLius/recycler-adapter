def isJava = project.plugins.hasPlugin(JavaPlugin)
def artifactId = getArtifactId()
if (isJava) {
    apply plugin: 'maven'
} else {
    apply plugin: 'com.github.dcendents.android-maven'
}
uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('SNAPSHOTS') && !project.version.endsWith('SNAPSHOT')) {
                project.version = "${project.version}-SNAPSHOT"
                println "snapshots version=${project.version}"
            }

            pom.version = project.version
            pom.groupId = project.group
            pom.artifactId = artifactId
            pom.project {
                licenses {
                    license {
                        name config.license.name
                        url config.license.url
                    }
                }
                developers {
                    developer {
                        id config.developer.id
                        name config.developer.name
                        email config.developer.email
                    }
                }
            }

            def userName = project.hasProperty('USERNAME') ? project.USERNAME : config.maven.userName
            def password = project.hasProperty('PASSWORD') ? project.PASSWORD : config.maven.password
            assert userName != null
            assert password != null
            // fileRepos
            if (project.hasProperty('FILE_REPOS')) {
                repository(url: config.maven.fileRepos)
            }
            // releases
            repository(url: config.maven.releasesUrl) {
                authentication(userName: userName, password: password)
            }
            // snapshot
            snapshotRepository(url: config.maven.snapshotsUrl) {
                authentication(userName: userName, password: password)
            }
        }
    }
}

def getArtifactId() {
    def selfConfig = project.hasProperty('artifactId')
    def artifactId = selfConfig ? project.artifactId : project.name
    println "project artifactId now is ${artifactId}. Use ext {artifactId = xxx} to customizing."
    return artifactId
}